mod MAUDE-NPA-JSON is
	protecting DEFINITION-PROTOCOL-RULES-HANDLING .
	protecting MAUDE-NPA .
	protecting CONVERSION .

	***(Order relation:
		Nat < IdElem <   Id   < IdSystem < IdSystemSet < IdSystemSet-or-Error
					   System
	***)
	var bound : Bound .
	var ghostList : GhostList .
	var freshSet : FreshSet .
	var id : Id .
	var idElem : IdElem .
	var idSys : IdSystem .
	var idSysSet : IdSystemSet .
	var idSysSetOrErr : IdSystemSet-or-Error .
	var intruderK : IntruderKnowledge .
	var lazyLearnt : LazyLearnt .
	vars msgA msgB : Msg .
	var n : Nat .
	var props : Properties .
	vars sMsgA sMsgB : SMsg .
	var sMsgElem : SMsgElem .
	var sMsgList : SMsgList .
	var sLMsgList : SMsgList-L$ .
	var sRMsgList : SMsgList-R$ .
	vars strandA strandB : Strand$ .
	var strandSet : StrandSet$ .
	var str : String .
	var sys : System .
	var t : Term .

	--- Includes quotes to achieve the additional text indirection
	op unparseTerm : Term -> String .
	eq unparseTerm(t) = "\"" + qidList2string(metaPrettyPrint(['MAUDE-NPA], t, none)) + "\"" .

	--- Tells whether a System Message is contained within the present conversation-known messages list
	op isSMsgInRMsgList : SMsg SMsgList-R$ -> Bool .
	eq isSMsgInRMsgList(sMsgA, nil) = false .
	eq isSMsgInRMsgList(sMsgA, (sMsgA, sRMsgList)) = true .
	eq isSMsgInRMsgList(sMsgA, (sMsgB, sRMsgList)) = isSMsgInRMsgList(sMsgA, sRMsgList) [owise] .

	--- Finds the signature of unique symbols used to mark search states as unable to reach initial states
	op findMsgSignature : SMsg StrandSet$ -> String .
	eq findMsgSignature(sMsgA, empty) = "Unknown" .
	eq findMsgSignature(sMsgA, (:: freshSet :: [sLMsgList | sRMsgList]) & strandSet) =
		if isSMsgInRMsgList(sMsgA, sRMsgList)
		then
			if (freshSet == nil)
			then "\"nil\""
			else unparseTerm(upTerm(freshSet))
			fi
		else findMsgSignature(sMsgA, strandSet)
		fi [owise] .

	--- Returns "true" iff Msg is contained in IntruderKnowledge, but only among the self state's knowledge ('inI' tag)
	op stringifyIsInIntruderK : Msg IntruderKnowledge -> String .
	eq stringifyIsInIntruderK(msgA, empty) = "false" .
	eq stringifyIsInIntruderK(msgA, msgB inI, intruderK) = if (msgA == msgB) then "true" else stringifyIsInIntruderK(msgA, intruderK) fi .
	eq stringifyIsInIntruderK(msgA, msgB !inI, intruderK) = stringifyIsInIntruderK(msgA, intruderK) .

	--- Fixes incorrect format caused by empty intruder data in NSPK state [1 . 5 . 2 . 7 . 2 . 4 . 2] (bug?)
	eq stringifyIsInIntruderK(msgA, intruderK) = "false" [owise] .

	op unparseMsgSeqList : StrandSet$ IntruderKnowledge SMsgList -> String .
	eq unparseMsgSeqList(strandSet, intruderK, +(msgA)) = "{ \"isSend\": true, \"msg\": " + unparseTerm(upTerm(msgA)) + ", \"signature\": " + findMsgSignature(+(msgA), strandSet) + ", \"isInIntruderK\": " + stringifyIsInIntruderK(msgA, removeIrr(msgA, intruderK)) + " }" .
	eq unparseMsgSeqList(strandSet, intruderK, -(msgA)) = "{ \"isSend\": false, \"msg\": " + unparseTerm(upTerm(msgA)) + ", \"signature\": " + findMsgSignature(-(msgA), strandSet) + ", \"isInIntruderK\": " + stringifyIsInIntruderK(msgA, removeIrr(msgA, intruderK)) + " }" .
	eq unparseMsgSeqList(strandSet, intruderK, lazyLearnt) = "" . 
	eq unparseMsgSeqList(strandSet, intruderK, sMsgElem, sMsgList) = unparseMsgSeqList(strandSet, intruderK, sMsgElem) + ", " + unparseMsgSeqList(strandSet, intruderK, sMsgList) [owise] .

	--- Given a System, returns a JSON representation
	op unparseSystem : System -> String .
	eq unparseSystem(strandSet || intruderK || sMsgList || ghostList || props) =
		"{ \"strandSet\": " + unparseTerm(upTerm(strandSet))
		+ ", \"intruderKnowledge\": " + unparseTerm(upTerm(intruderK))
		+ ", \"msgSeqList\": [" + unparseMsgSeqList(strandSet, intruderK, sMsgList)
		+ "], \"ghostList\": " + (if ghostList =/= nil then unparseTerm(upTerm(ghostList)) else "null" fi)
		+ ", \"props\": " + (if props =/= nil then unparseTerm(upTerm(props)) else "null" fi) + " }" .

	--- Given an Id, returns a JSON representation.
	--- Each JSON object contains a main identifier (key "elem") and, if present, an internal sub-identifier (key "subElem").
	op unparseId : Id -> String .
	eq unparseId(n) = "{ \"elem\": " + string(n, 10) + " }" .
	eq unparseId(idElem{n}) = "{ \"elem\": " + string(idElem, 10) + ", \"subElem\": " + string(n, 10) + " }" .
	eq unparseId(idElem . id) = unparseId(idElem) + ", " + unparseId(id) .

	--- Given an IdSystemSet, returns a JSON representation.
	--- Each JSON object contains an "id", which is a list of identifier elements, and a message.
	op unparseISS : IdSystemSet -> String .
	eq unparseISS(empty) = "" .
	eq unparseISS((< id > (sys))) = "{ \"isInitial\": "
		+ (if isInitial(sys) then "true" else "false" fi)
		+ ", \"id\": [" + unparseId(id) + "], \"system\": " + unparseSystem(sys) + " }" .
	eq unparseISS((idSys) idSysSet) = unparseISS((idSys)) + ", " + unparseISS(idSysSet) [owise] .

	--- Given a high-level protocol computation result, returns a JSON representation
	op toJSON : IdSystemSet-or-Error -> String .
	eq toJSON(idSysSet) = "[" + unparseISS(idSysSet) + "]" .
	eq toJSON(idSysSetOrErr) = "[]" [owise] .

	op runJSON[_]`(_`, _`) : Id-SMsgList Nat Bound -> String .
	eq runJSON[id:Id-SMsgList](n, bound) = toJSON(debug[id:Id-SMsgList](n, bound)) .

	--- A simple convenience wrapper to toJSON
	op runJSON : Nat Bound -> String .
	eq runJSON(n, bound) = runJSON[1](n, bound) .

	op countAttackStatesFrom : Nat -> Nat .
	eq countAttackStatesFrom(n) = if extractAttack(n) :: Attack then 1 + countAttackStatesFrom(n + 1) else 0 fi .

	--- Counts the number of current protocol's attack states (start of backwards search)
	op countAttackStates : -> Nat .
	eq countAttackStates = countAttackStatesFrom(0) .
endm
