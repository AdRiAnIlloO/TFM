mod MAUDE-NPA-JSON is
	protecting DEFINITION-PROTOCOL-RULES-HANDLING .
	protecting MAUDE-NPA .
	protecting CONVERSION .

	***(Order relation:
		Nat < IdElem <   Id   < IdSystem < IdSystemSet < IdSystemSet-or-Error
					   System
	***)
	var N : Nat .
	var B : Bound .
	var IE : IdElem .
	var I : Id .
	var S : System .
	var IS : IdSystem .
	var ISS : IdSystemSet .
	var ISSoE : IdSystemSet-or-Error .

	--- Given a System, returns a JSON representation
	op toJSON : System -> String .
	eq toJSON(S) = qidList2string(metaPrettyPrint(['MAUDE-NPA], upTerm(S), none)) .

	--- Given an Id, returns a JSON representation.
	--- Each JSON object contains a main identifier (key "elem") and, if present, an internal sub-identifier (key "subElem").
	op unparseId : Id -> String .
	eq unparseId(N) = "{ \"elem\": " + string(N, 10) + " }" .
	eq unparseId(IE{N}) = "{ \"elem\": " + string(IE, 10) + ", \"subElem\": " + string(N, 10) + " }" .
	eq unparseId(IE . I) = unparseId(IE) + ", " + unparseId(I) .

	--- Given an IdSystemSet, returns a JSON representation.
	--- Each JSON object contains an "id", which is a list of identifier elements, and a message.
	op unparseISS : IdSystemSet -> String .
	eq unparseISS(empty) = "" .
	eq unparseISS((< I > (S))) = "{ \"id\": [" + unparseId(I) + "], \"msg\": \"" + toJSON(S) + "\" }" .
	eq unparseISS((IS) ISS) = unparseISS((IS)) + ", " + unparseISS(ISS) [owise] .

	--- Given a high-level protocol computation result, returns a JSON representation
	op toJSON : IdSystemSet-or-Error -> String .
	eq toJSON(ISS) = "[" + unparseISS(ISS) + "]" .
	eq toJSON(ISSoE) = "[]" [owise] .
	
	op runJSON[_]`(_`, _`) : Id-SMsgList Nat Bound -> String .
	eq runJSON[I:Id-SMsgList](N, B) = toJSON(debug[I:Id-SMsgList](N, B)) .

	--- A simple convenience wrapper to toJSON
	op runJSON : Nat Bound -> String .
	eq runJSON(N, B) = runJSON[1](N, B) .
	
	op countAttackStatesFrom : Nat -> Nat .
	eq countAttackStatesFrom(N) =
		if extractAttack(N) :: Attack
		then 1 + countAttackStatesFrom(N + 1)
		else 0
		fi .
	
	op countAttackStates : -> Nat .
	eq countAttackStates = countAttackStatesFrom(0) .
endm
